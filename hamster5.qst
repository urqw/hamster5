:10
proc init
pln #%gametitle$
btn introduction, Начать игру
btn autors, Автор(ы)
btn instructions, Инструкции
end

:init
perkill
invkill
gametitle="Похождения отважного хомяка Семена - 5: На дороге"
где_крышка = 1
прокопал_нору = 1
кот_расстояние = 2
end

:autors
pln Идея____________Triangle
pln Программист_____Apromix
pln Сценарий________Apromix
pln Наборщик________Apromix
pln В главной роли__Ныне покойный хомяк Семен
proc contacts
btn 10, Назад
log = "Text"
end

:contacts
pln
pln Пишите человеку который придумал Семена на triangle@zmail.ru 
pln Пишите мне на apromix@mail.ru или bees@meta.ua
end

:instructions
pln Тут инструкции не нужны! И так все понятно!
btn 10, Назад
end

:introduction
proc init
pln Вы - хомяк по кличке Семен. Тот еще крепкий орешек! Если Вы прошли предыдущие игры из серии, то должны помнить, что Семена коварно заточили в клетку, но ценой неимоверных усилий отважному хомяку удалось вырваться на свободу... В конце-концов он оказался за забором на пыльной дороге... Пока все.
btn game, Играть!
end

:game
pln Вы стоите на дороге совсем не далеко от того места, где с помощью двух мышей перебрались через забор. По левую лапу от Вас дорога бежит в даль и видно большую рощу. По правую лапу возле дороги стоит огромный мусорный бак и возле него ящик. С высокого забора свисает длинная веревка, но ее конец развивается на ветру и Вам за него не ухватиться.
if прислушался = 1 then pln Значит кот рядом! Вы понимаете, что если сейчас побежите, то рано или поздно окажетесь в его лапах. Нужно срочно что-то предпринять...
btn garb, Бежать к мусорному баку
btn road, Бежать по дороге
btn slush2, Прислушаться
end

:garb
где_хомяк = 0
if глоток_воздуха = 1 then p Глоток свежего воздуха и Ваши силы восстановились. &глоток_воздуха = 0
p Возле забора у края дороги стоит старый мусорный бак и большой ящик. На дороге - лужа с водой. 
p В траве белеет жевачка. 
if Жевачка then p Но у Вас уже есть один кусочек. 
if not Щепка then p Под ногами у Вас лежит щепка. &btn sliver, Взять щепку
if not Жевачка then btn gum, Взять жевачку
btn box, Залезть на ящик
if прокопал_нору = 2 then btn bak2, Пробраться через норку в мусорный бак
btn lujja, Подойти к луже воды
end

:lujja
p Вы подбегаете к луже. Вода в ней имеет отвратительный цвет и отдает тиной.
if not Вода then btn woda, Наполнить водой щеку
btn pit, Выпить воды
btn garb, Назад к баку
end

:pit
p Вы с наслаждением лакаете воду из лужи, но вскорее Вам становится плохо и Вы умираете.
btn introduction, Сначала
end

:woda
p Вы заливаете воду за щеку, и она медленно, но верно раздувается. А как иначе?
inv+ Вода
btn lujja, Назад
end

:gum
if not Жевачка then pln Вы отгрызли себе кусочек на память.&inv+ Жевачка
btn garb, Назад
end

:box
if где_хомяк = 1 then p Вы на ящике у дороги. 
if где_хомяк = 0 then p Вы с трудом взбираетесь на ящик, несколько раз чуть не упав. После такого тяжелого подъема добраться до крышки бака пустячное дело. &где_хомяк = 1
if где_хомяк = 2 then p Вы осторожно спустились на ящик. &где_хомяк = 1
if бак_закрыт = 1 then p Бак закрыт крышкой. 
if отгрыз_бумажку = 0 then p В углу лежит старая газета. else p В углу лежит старая погрызенная газета.
if not Бумажка then btn bum, Отгрызть кусочек бумажки
btn tk, Взобраться на крышку бака
btn garb, Спуститься на землю
end

:bum
pln Вы отгрызаете кусочек бумажки и запихиваете его за щеку.
отгрыз_бумажку = 1
inv+ Бумажка
btn box, Осмотреться
end

:bak
if где_крышка = 0 and бак_закрыт = 0 then p Вы прыгаете в бак и крышка с грохотом захлопывается над Вами.&бак_закрыт = 1 
if где_крышка = 0 and прокопал_нору < 2 and бак_закрыт = 0 then p Вам никогда не выбраться из бака. Вы оказались в смертельной ловушке!&btn introduction, Сначала&end

:bak2
if где_крышка > 0 then p Металлические стенки мусорного бака опасно возвышаются вокруг Вас. 
if где_крышка = 2 then Но еще более опасна крышка бака, готовая в любой момент захлопнуться. 
p Внутри тесно, 
if бак_закрыт = 1 then p темно, 
p сыро. От разных запахов кружится голова. 
if сила = 0 then p Но один острый запах семечек беспокоит Вас. &btn sem, Искать семечки
p Вы себя неловко чувствуете в этом замкнутом пространстве. 
if бак_закрыт = 1 then p Металлическая крышка у Вас над головой плотно закрыта. 
if прокопал_нору = 2 then p В самом низу сквозь прорытый Вами туннель пробивается свет. else p Внизу небольшая часть стенки бака проржавела и внутрь ввалилась свежая земля. 
p Здесь же лежит старый разбитый радиоприемник и еще много различного мусора. 
if not Стекло then p Под ногами у Вас лежат куски стекла от разбитой вазы.&btn gl, Взять один кусочек стекла
btn osm, Осмотреть радиоприемник
if где_крышка > 0 then глоток_воздуха = 1&btn tk, Выбраться из бака
if Пластинка then btn kop, Копать туннель
if прокопал_нору = 2 then btn podk, Нырнуть в норку
end

:bak3
if бак_закрыт = 1 then p Вы ловко спрыгиваете на крышку и затем пытаетесь пробраться в бак. В этот раз кот оказывается быстрее...&btn introduction, Сначала&end
кот_расстояние = 0
p Вы ловко спрыгиваете на крышку и затем в бак. Кот устремляется за Вами 
if прокопал_нору < 2 then p и настигает Вас!&btn introduction, Сначала&end
if прокопал_нору = 2 then p , но Вы в это время уже бежите по норке к выходу. Не обнаружив Вас внутри мусорного бака, кот пытается выбраться, а Вам остается только одно...
if кот_расстояние = 0 and прилепил_веревку = 2 and где_крышка = 0 then btn fin, Дернуть за веревку else btn road, Бежать со всех ног!..
end

:osm
p Вы ничего не понимаете в радиоприемниках, 
if отвинтил_гайку = 0 then p но несколько металлических деталей Вас очень интересуют... 
if отвинтил_гайку = 0 then p Вы крутитесь вокруг, пробуете все на зуб и так, и эдак, и вдруг замечаете, что одна гайка легко крутится в Ваших лапках. Она прижимает маленькую алюминиевую пластинку. 
if отвинтил_гайку = 0 then btn gay, Отвинтить гайку
if отвинтил_гайку = 1 then p и Вам становится очень скучно и одиноко. 
if отвинтил_гайку = 1 and not Пластинка and взял_пластину = 0 then p Под ногами у Вас лежит алюминиевая пластинка. &btn alp, Взять пластинку
if отвинтил_гайку = 1 and not Гайка and взял_гайку = 0 then p Рядом лежит гайка. &btn gaj, Взять гайку
btn bak, Назад
end

:sem
p Вы резво рыскаете по содержимому мусорного бака, пока не находите разорванный пакет. И в нем - семечки! Их всего несколько штук, но Вам большего и не надо!
btn eat, Съесть семечки
btn bak, Назад
end

:eat
p Вы решаете, что подкрепиться никогда не поздно и съедаете все семечки. Вы сразу чувствуете, как Ваше крохотное тельце наливается чудовищной силой!
сила = 1
btn bak, Назад
end

:gaj
p Вы запихиваете за щеку металлическую гайку.
inv+ Гайка
взял_гайку = 1
btn osm, Осмотреться
end

:gay
p Вы отвинчиваете гайку и она падает в мусор. Рядом с ней падает небольшая металлическая пластинка.
отвинтил_гайку = 1
btn osm, Осмотреться
end

:tk
где_хомяк = 2
if глоток_воздуха = 1 then p Глоток свежего воздуха - и Вы в порядке. &глоток_воздуха = 0
if бак_закрыт = 1 then p Вы стоите на крышке бака, но отсюда внутрь бака не попасть никак!..&goto tk2
p Вы стоите на краю крышки бака и с интересом заглядываете в мусорный бак. 
if где_крышка <> 1 then p Крышка под Вашим весом сильно наклонилась и Вы с трудом удерживаетесь на ее скользкой металлической поверхности. 
btn bak, Нырнуть в бак
btn right, Сдвинуть крышку бака вправо
btn left, Сдвинуть крышку бака влево
:tk2
if прилепил_веревку = 1 then p На ветру колышится веревка. 
if прилепил_веревку = 2 then p Веревка прикреплена к крышке бака. 
btn box, Спрыгнуть на ящик
if сила = 1 then btn zabor, Запрыгнуть на забор
if кот_расстояние = 2 then btn slush, Прислушаться
if бак_закрыт = 0 and Жевачка and Веревка and прилепил_жевачку = 0 then btn fh, Прилепить жевачку к крышке бака
if бак_закрыт = 0 and Веревка and прилепил_жевачку = 1 and прилепил_веревку = 0 then btn fd, Прикрепть веревку к жвачке
if прилепил_веревку = 1 and взял_гайку = 1 then btn fgaj, Привязать гайку к веревке
end

:fgaj
прилепил_веревку = 2
p Вы привязываете к концу веревки тяжелую гайку и веревка больше не колышится на ветру.
inv- Гайка
btn tk, Назад
end

:fh
p Вы прилепили жевачку к крышке бака.
inv- Жевачка
прилепил_жевачку = 1
btn tk, Назад
end

:fd
p Вы прикрепляете конец веревки к жевачке.
inv- Веревка
прилепил_веревку = 1
btn tk, Назад
end

:right
if Щепка and где_крышка < 2 then pln Используя щепку как рычаг Вы еще сдвигаете крышку бака на край.&где_крышка = где_крышка + 1 else pln Вы пыхтите и тужитесь, но крышка слишком тяжела и не поддается.
btn tk, Залезть на крышку бака
end

:zabor
if not Веревка then p На ветру колышится короткий кусок нити. Но для Вас это целая веревка!&if Стекло and взял_веревку = 0 then btn vrk, Отрезать веревку
btn house, Осмотреть двор
btn tk, Спрыгнуть назад на крышку бака
end

:vrk
p Ловко орудуя стеклом, словно ножом, Вы отрезаете кусок веревки и обматываетесь ею. Внезапно стекло выпадает из Ваших лап...
взял_веревку = 1
inv+ Веревка&inv- Стекло
btn zabor, Назад
end

:slush
p Вы слышите мяуканье кота за забором. Старые страхи просыпаются. 
btn tk, Назад
end

:slush2
pln Вы слышите, как за забором возится кот, пытаясь Вас найти...
прислушался = 1
btn game, Назад
end

:house
p Как же здесь высоко! Вы видите слева дорогу, дальше рощу, а справа огромный дом и большой двор. А во дворе - кота! 
if кот_расстояние = 2 then p Старый враг все еще ищет Вас. 
if кот_расстояние = 1 then p Он стоит у забора и пристально смотрит на Вас. 
if Стекло and кот_расстояние = 2 then btn zaj, Пустить солнечного зайчика
if Щепка then btn ghj, Пригрозить коту щепкой
if Вода and кот_расстояние = 1 then btn purs, Прыснуть в кота водой
if кот_расстояние > 0 then btn krik, Закричать на кота
btn zabor, Затаиться
end

:ghj
p Вы пригрозили коту щепкой, считая что Вы самый сильный в мире хомяк. 
if кот_расстояние = 2 then p Но кот не обратил на Вас внимания...
if кот_расстояние = 1 then p Но кот просто смотрит на Вас...
btn house, Назад
end

:krik
if кот_расстояние = 2 then p Вы смело кричите на кота, чтобы он Вас заметил, но он слишком занят и не замечает Вас...
if кот_расстояние = 1 then p Набравшись смелости и взглянув в большие бездонные глазищи, Вы кричите на кота, но он только смотрит на Вас...
btn house, Назад
end

:zaj
p Вы достаете кусок стекла и пускаете солнечного зайчика. Солнце слепит коту глаза и он обращает на Вас внимание. Еще миг - и кот у забора, но не решается прыгнуть.
кот_расстояние = 1
btn house, Замереть
end

:purs
p Вы прицеливаетесь и... прыскаете водой в кота! Мокрый кот в этот раз решает не упустить обидчика. Он бросается на Вас. А Вы?..
inv- Вода
btn road, Кинуться прочь по дороге
btn bak3, Спрыгнуть в мусорный бак
end

:fin
p Вы ловите лапками гайку и сильно дергаете за веревку. Крышка бака с грохотом встает на свое место. Вы слышите жалобное мяуканье. Все! Ловушка захлопнулась! Кот заперт в мусорном баке! 
бак_закрыт = 1
btn road, Бежать по дороге
end

:road
p Вы бежите по дороге навстречу неизвестному... 
if кот_расстояние = 0 and бак_закрыт = 1 then p И дом, и двор, и кот в мусорном баке остаются далеко позади... &proc final else p Но внезапно на Вас набрасывается голодный кот!..&btn introduction, Сначала&end
end

:podk
p Вы ныряете в свой туннель и в мгновенье ока оказываетесь снаружи.
глоток_воздуха = 1
btn garb, Выбраться из норки
end

:kop
p Вооружившись алюминиевой пластиной, Вы быстро прокапываете нору наружу.
inv- Пластинка
прокопал_нору = 2
глоток_воздуха = 1
btn garb, Выбраться из норки
end

:left
if Щепка and где_крышка > 0 then pln Используя щепку как рычаг Вы еще сдвигаете крышку бака на край.&где_крышка = где_крышка - 1 else pln Вы пыхтите и тужитесь, но крышка слишком тяжела и не поддается.
btn tk, Залезть на крышку бака
end

:sliver
if not Бумажка then pln Вы запихиваете острую щепку за щеку, и она с легкостью ее протыкает. Вы быстро истекаете кровью...&btn introduction, Сначала&end
if Бумажка then pln Вы заворачиваете щепку в бумажку и только после этого запихиваете щепку за щеку.
inv- Бумажка
inv+ Щепка
btn garb, Назад
end

:alp
if not Бумажка then pln Вы запихиваете острую пластинку за щеку, и она с легкостью ее прорезает. Вы быстро истекаете кровью...&btn introduction, Сначала&end
if Бумажка then pln Вы заворачиваете алюминиевую пластинку в бумажку и только после этого запихиваете ее за щеку.
inv- Бумажка
inv+ Пластинка
взял_пластину = 1
btn osm, Назад
end

:gl
if not Бумажка then pln Вы запихиваете острый кусок стекла за щеку, и он с легкостью ее протыкает. Вы быстро истекаете кровью...&btn introduction, Сначала&end
if Бумажка then pln Вы заворачиваете кусок стекла в бумажку и только после этого запихиваете его за щеку.
inv- Бумажка
inv+ Стекло
btn bak, Назад
end

:final
pln
pln
pln Поздравляю! Вы благополучно миновали все опасности и прошли пятую серию приключений хомяка Семена! Благодаря Вам Семен остался жив! Пришло время пройти шестую часть серии: Похождения отважного хомяка Семена-6: В роще!
proc contacts
end